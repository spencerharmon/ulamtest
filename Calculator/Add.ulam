/**
	\author Spencer Harmon
	\license lgpl
	\symbol Ad
	\symmetries all
	\color #b52
	
 */
element Add : Agent{
	Int value;

	typedef EventWindow.SiteNum SiteNum;
	
	Bool noCopiesNearMe(){
		for(SiteNum i = 1; i < 41; ++i){
			Atom a = ew[i];
			if(a as Add){
				if(a.child == child && a.parent == parent && a.value == value){
					return false;
				}
			}
		}
		return true;
	}
	Atom me(){
		return self;
	}
	Int typeFunction(Atom c){
		Int ret;
		if(c as Add){
			ret = value + c.value;
		}
		return ret;
	}
	Atom evaluateParenthesis(ID c, ID p){
		OpenParenth op;
		op.value = value;
		op.parent = p;
		op.child = c;
		op.evaluated = true;
		return op;
	}
	Void overwrite(Int val, ID p, ID c, SiteNum slot){
		Atom a = ew[slot];
		if(a as Add){
			Add ad;
			ad.value = val;
			ad.parent = p;
			ad.child = c;

			ew[slot] = ad.atomof;
		}
	}

	Void behave(){
		SiteNum cSlot = findChild();
		SiteNum pSlot = findParent();
		if(!collapse(cSlot,pSlot)){
			clusterDiffuse();
		}
	}
}
