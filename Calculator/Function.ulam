/**
	\author Spencer Harmon
	\license lgpl
	\symbol Fn
	\color #82d
	\symmetries all
	
 */
element Function : Agent{

	Int value;

	typedef EventWindow.SiteNum SiteNum;

	Bool noCopiesNearMe(){
		for(SiteNum i = 1; i < 41; ++i){
			Atom a = ew[i];
			if(a as Function){
				if(a.child == child && a.parent == parent && a.value == value){
					return false;
				}
			}
		}
		return true;
	}
	Atom me(){
		return self;
	}
	Int typeFunction(Atom a){
		Int ret;
		return ret;
	}
	Int typeFunction(Atom a, Atom b){
		Int notvirtual;
		return notvirtual;
	}
	Atom evaluateParenthesis(ID n, ID a){
		Empty e;
		return e;
	}
	Void overwrite(Int i, ID p, ID c, SiteNum s1, SiteNum s2){
		Bool virt = false;
	}
	Void overwrite(Int i, ID p, ID c, SiteNum s1){
		Bool virt = false;
	}
	Void expireCopy(){
		//Function atoms cannot expire themselves when propogating since they need to preserve the parent and update themselves and the child.
		Bool expired = false;
		for(SiteNum i = 1; i < 41; ++i){
			Atom a = ew[i];
			if(a as Function){
				if(a.parent == parent && a.child == child){
					Atom e = expire();
					ew[i] = e;
					expired = true;
				}
			}
		}
		if(!expired){
			//violate dreg/res and use empty space.
			SiteNum slot = cl.emptySlotNearMe();
			Atom e = expire();
			ew[slot] = e;
		}
	}
	Bool propogateType(SiteNum cSlot){
		Bool collapsed = false;
		SiteNum slot = cl.emptySlotNearMe();
		Atom c = ew[cSlot];
		if(value == 1){
			if(c as Data){
				Add ad;
				ad.parent = parent;
				ad.child = child;

				ad.value = c.value;
				
				Function fn;
				fn.value = value;
				fn.child = c.child;
				fn.parent = c.parent;
				
				collapsed = true;
				ew[cSlot] = ad;
				ew[0] = fn;
				expireCopy();
			}else if(c as CloseParenth){
				CloseParenth cp;
				cp.parent = parent;
				cp.child = c.child;
				ew[cSlot] = cp;

				collapsed = true;
				Atom e = expire();
				ew[0] = e;
			}
		}else if(value == 2){
			if(c as Data){
				Subtract sb;
				sb.parent = parent;
				sb.child = child;

				sb.value = c.value;
				
				Function fn;
				fn.value = value;
				fn.child = c.child;
				fn.parent = c.parent;

				ew[cSlot] = sb;
				ew[0] = fn;
				expireCopy();
			}else if(c as CloseParenth){
				CloseParenth cp;
				cp.parent = parent;
				cp.child = c.child;
				ew[cSlot] = cp;

				collapsed = true;
				Atom e = expire();
				ew[0] = e;
			}
		}else if(value == 3){
			if(c as Data){
				Divide dv;
				dv.parent = parent;
				dv.child = child;

				dv.value = c.value;
				
				Function fn;
				fn.value = value;
				fn.child = c.child;
				fn.parent = c.parent;

				ew[0] = fn;
				ew[cSlot] = dv;
				expireCopy();
			}else if(c as CloseParenth){
				CloseParenth cp;
				cp.parent = parent;
				cp.child = c.child;

				collapsed = true;
				ew[cSlot] = cp;
				Atom e = expire();
				ew[0] = e;
			}
		}
		return collapsed;
	}
	Void behave(){
		SiteNum cSlot = findChild();
		if(cSlot > 0){
			if(!propogateType(cSlot)){
				clusterDiffuse();
			}
		}else{
			clusterDiffuse();
		}
	}
}
