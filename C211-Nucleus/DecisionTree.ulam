/**
  The idea is to construct a decision tree using Genes
  
  \author Spencer Harmon
  \license lgpl
 */
quark DecisionTree{
  Random r;
  DebugUtils du;
  EventWindow ew;
  AtomUtils au;

  typedef Cell.Seqno StoredValue;

  typedef AtomUtils.Type ElementType;
  typedef WindowScanner.Category Category;
  typedef Cell.Seqno Seqno;
  typedef Cell.ID ID;
  typedef EventWindow.SiteNum SiteNum;

  Atom findSeqno(Seqno seqno, ID id){
    Empty e;

    for(SiteNum i = 1; i <=40; ++i){
      Atom gene = ew[i];
      if(gene as Gene){
        if(gene.id == id && gene.seqno == seqno){
          return gene;
        }
      }
    }
    return e;    
  }
  StoredValue evaluate(Seqno var, ID id, StoredValue comIn){
    Bool evaluated[40];
    Bool checked[40];

    StoredValue value[40];
    Seqno parent[40];

    Unsigned(2) op[40];

    Bits(2) tree0Type[40];
    StoredValue eqTree0[40];

    Bits(2) tree1Type[40];
    StoredValue eqTree1[40];

    Bool keepRecursing = true;
    Seqno seqno = var;

    Unsigned(2) dejavu;
    while(keepRecursing){
      if(evaluated[var]){
        return value[var];
      }
      if(!evaluated[seqno] && !checked[seqno]){
        //
        //Populate some stuff.
        //
        Atom gene = findSeqno(seqno, id);
        
        //will only return here if checked wasn't set because gene
        //seqno wasn't found. 
        //dejavu tells us if we're scanning one too many times.
        ++dejavu;
        if(dejavu > 1){
          return (StoredValue) 0;
        }
        if(gene as Gene){
          checked[seqno] = true;

          op[seqno] = gene.op;

          tree0Type[seqno] = gene.tree0Type;
          eqTree0[seqno] = gene.eqTree0;

          tree1Type[seqno] = gene.tree1Type;
          eqTree1[seqno] = gene.eqTree1;
        }
      }
      //
      // prepare comIn value and call it a terminator
      //
      if(tree0Type[seqno] == 1 && eqTree0[seqno] == 0){
        tree0Type[seqno] = 0;
        eqTree0[seqno] = comIn;
      }
      if(tree1Type[seqno] == 1 && eqTree1[seqno] == 0){
        tree0Type[seqno] = 0;
        eqTree0[seqno] = comIn;
      }
      //
      // Do maths operation if child nodes are terminator type.
      // Pass control back to parent.
      //
      if(tree0Type[seqno] == 0 && tree1Type[seqno] == 0){
        value[seqno] = operate(eqTree0[seqno], op[seqno], eqTree1[seqno]);
        evaluated[seqno] = true;
        keepRecursing = true;
        seqno = parent[seqno];
      }
      //
      // Collapse child nodes and recurse
      //
      if(tree0Type[seqno] == 1){
        Seqno childseqno = (Seqno)eqTree0[seqno];
        if(!evaluated[childseqno]){
          parent[childseqno] = seqno;
          seqno = childseqno;
          keepRecursing = true;
        }
        else{
          tree0Type[seqno] = 0;
          eqTree0[seqno] = value[childseqno];
          keepRecursing = true;
        }  
      }
      else{
        if(tree1Type[seqno] == 1){
          Seqno childseqno = (Seqno)eqTree1[seqno];
          if(!evaluated[childseqno]){
            parent[childseqno] = seqno;
            seqno = childseqno;
            keepRecursing = true;
          }
          else{
            tree1Type[seqno] = 0;
            eqTree1[seqno] = value[childseqno];
            keepRecursing = true;
          }
        }
      }
    }// while keepRecursing loop
    //g++ complains without returning something at this namespace, even though
    //control shouldn't reach here.
    return value[var];
  }//evaluate() namespace
  StoredValue selfEvaluate(Seqno var, ID id, StoredValue comIn){
    StoredValue val0;
    StoredValue val1;
    StoredValue value;
    Atom gene;
    Atom my = ew[0];
    if(my as Gene){
      if(my.tree0Type == 1 && my.eqTree0 == 0){
        my.tree0Type = 0;
        val0 = comIn;
      }else if(my.tree0Type == 1 && my.eqTree0 != 0){
        gene = findSeqno((Seqno)my.eqTree0, id);
        if(gene as Gene){
          val0 = gene.value;
        }
      }else if(my.tree0Type == 0){
        val0 = my.eqTree0;
      }

      if(my.tree1Type == 1 && my.eqTree1 == 0){
        my.tree0Type = 0;
        val1 = comIn;
      }else if(my.tree1Type == 1 && my.eqTree1 !=0){
        gene = findSeqno((Seqno)my.eqTree1, id);
        if(gene as Gene){
          val1 = gene.value;
        }
      }else if(my.tree1Type == 0){
        val1 = my.eqTree1;
      }
      value = operate(val0, my.op, val1);
    }
    return value;
  }//selfEvaluate() namespace
  StoredValue operate(StoredValue val1, Unsigned(2) op, StoredValue val2){
    StoredValue value;
    if(op == 0){
      value = (StoredValue)(val1 + val2);
    }
    if(op == 1){
      value = (StoredValue)(val1 - val2);
    }
    if(op == 2){
      value = (StoredValue)(val1 * val2);
    }
    if(op == 3){
      value = (StoredValue)(val1 / val2);
    }
    return value;
  }
}//DecisionTree namespace
