/**
  \author Spencer Harmon
  \license lgpl
  \symbol Ge
  \color #7ee
  \symmetries all
 */
element Gene : QGene{
  Seqno seqno;
  Seqno treeno;

  Void behave(){
    value = dt.selfEvaluate();
    killCopies();
    Bool touchingMembrane = false;
    SiteNum slot = 0;
    for(SiteNum i = 4; i > 0; --i){
      Atom a = ew[i];
      if(a as InnerMembrane){
        if(a.set){
          touchingMembrane = true;
          ew.changeSymmetry(a.sym);
        }
      }
    }
    if(touchingMembrane){
      Atom s = ew[1];
      if(s as Empty){
        slot = (SiteNum) 1;
      }
    }else{
      
      for(SiteNum i = 4; i > 0; --i){
        Atom a = ew[i];
        if(a as Empty){
          slot = i;
        }
      }
    }
    if(slot != (SiteNum) 0 && !cl.canSeeTree(id, treeno)){
      ew.swap(0,slot);
    }else{
      cl.bringGenesCloser(id, treeno);
    }
  }
}
