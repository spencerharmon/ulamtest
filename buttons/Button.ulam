quark Button : TouchReporter{
  DebugUtils du;
  AtomUtils au;
  DregResUtils dru;
  Random r;
  typedef Agent.ID ID;  
  typedef EventWindow.SiteNum SiteNum;
  virtual ID produce(SiteNum slot);
  virtual Atom me();
  Bool center;
  Bool newID;
  ID id;
  Unsigned(4) timeout;
  Void propagateID(){
    --timeout;
    Bool propagated;
    if(newID){
      for(SiteNum i = 1; i <= 40; ++i){
        Atom a = ew[i];
        if(a as Button){
          if(a.id != id){
            a.id = id;
            a.newID = true;
            a.timeout = timeout.maxof;
            ew[i] = a.atomof;
            propagated = true;
          }
        }
      }
    }
    if(!propagated){
      newID = false;
    }
  }
  Void behave(){
    if(center){
      for(SiteNum i = 1; i <= 8; ++i){
        Atom a = ew[i];
        if(a as Empty){
          Atom n = au.new(au.getType(me()));
          if(n as Button){
            n.center = false;
            ew[i] = n.atomof;
          }
        }
      }
    }
    if(timeout == 0){
      if(touchReport()){
        for(SiteNum i = 1; i <=40; ++i){
          Atom a = ew[i];
          if(a as Res){
            newID = true;
            ID tmp;
            tmp = produce(i);
            id = tmp;
            timeout = timeout.maxof;
            break;
          }
        }
      }
    }
    propagateID();
  }
}